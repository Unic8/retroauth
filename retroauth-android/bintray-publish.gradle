apply plugin: 'maven'
// to read properties from gradle-local.properties
apply plugin: 'net.saliman.properties'
group = libraryGroup
version = libraryVersion

configurations {
	archives {
		extendsFrom configurations.default
	}
}

/**
 * pack sources jar
 */
task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

/**
 * add sources.jar to artifacts
 */
artifacts {
	archives androidSourcesJar
}


/**
 * upload aar and artifacts to local repository
 */
task installArchives(type: Upload) {
	description "Installs the artifacts to the local Maven repository."
	configuration = configurations['archives']
	repositories {
		mavenDeployer {
			repository(url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath)

			pom.project {
				name 'Android Retroauth Plugin'
				packaging 'aar'
				description 'Authentication library for retrofit 2 using android'
				url 'https://github.com/andretietz/retroauth'

				scm {
					url 'scm:git@github.com:andretietz/retroauth.git'
					connection 'scm:git@github.com:andretietz/retroauth.git'
					developerConnection 'scm:git@github.com:andretietz/retroauth.git'
				}
			}
		}
	}
}


uploadArchives {
	repositories {
		mavenDeployer {
			repository (
					id: 'java',
					url: 'https://api.bintray.com/maven/unicate/java/retroauth'
			) {
				authentication(
						userName: bintrayUser,
						password: bintrayPass
				)
			}

			pom.project {
				name 'Retroauth Library for Android'
				packaging 'aar'
				description 'This library, is an android extension for the retroauth library'
				url 'https://github.com/andretietz/retroauth'

				scm {
					url 'scm:git@github.com:andretietz/retroauth.git'
					connection 'scm:git@github.com:andretietz/retroauth.git'
					developerConnection 'scm:git@github.com:andretietz/retroauth.git'
				}
			}
		}
	}
}