apply plugin: 'com.android.library'

android {
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        absolutePaths false
        lintConfig rootProject.file("lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion '6.0'
}


task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile rootProject.file('checkstyle.xml')
    source = ['src/main/java', 'src/test/java']

    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    classpath = files()
    configProperties = [
            'proj.module.dir'      : projectDir.absolutePath,
            'checkstyle.cache.file': './build/cache/checkstyle-cache'
    ]
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:23.0.0'
    api project(':retroauth')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testImplementation "org.robolectric:robolectric:3.6"
    testImplementation 'org.mockito:mockito-core:2.0.111-beta'

}
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'
apply from: rootProject.file('gradle/publish.gradle')
